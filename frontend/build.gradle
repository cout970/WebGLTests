group 'com.cout970'
version '1.0-SNAPSHOT'
buildDir = '../build'

buildscript {
    ext.kotlin_version = '1.1.3-2'
    ext.html_version = "0.6.3"

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.20"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$html_version"
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/run/${project.name}.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}

kotlinFrontend {
    sourceMaps = false

    npm {
        replaceVersion("main", "backend.js")

        dependency("electron")
        dependency("react")
        dependency("react-dom")
        dependency("pug")
        dependency("less")
        dependency("electron-interceptor")
    }
}

sourceSets {
    main.kotlin.srcDirs += "../src/client/kotlin"
    main.resources.srcDirs += "../src/client/resources"

    main.output.resourcesDir = "../build/run"

    test.kotlin.srcDirs += "../src/test/kotlin"
    test.resources.srcDirs += "../src/test/resources"
}

task copyResources(type: Copy) {
    from sourceSets.main.resources.srcDirs
    into file(buildDir.path + "/run")
}

task buildFrontend(dependsOn: build){
    doLast{
        print "Frontend compiled"
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}